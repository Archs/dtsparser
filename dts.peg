package main

import "github.com/Archs/dtsparser/dts"

type Parser Peg {
	dts.DTS
}

start
	<- SPACE (ModuleDeclaration / ClassDeclaration / InterfaceDeclaration )+ eof

ModuleDeclaration
	<- modifier? ModuleIdentifier ModuleBody

ModuleBody
	<- block_start 
		   (ClassDeclaration
		   	/ InterfaceDeclaration
		   	/ ModuleDeclaration
			/ VariableDeclaration
			/ FuncDeclaration
			/ AssignmentDeclaration)*
	   block_end

AssignmentDeclaration
	<- AssignmentModifier? identifier? AssignmentSeparator AssignmentValue DeclarationSeperator

AssignmentModifier
	<- (('declare' / 'export' / 'private' / 'static' 
	    / 'import' / "var" / "function" / 'type') SPACE)+

AssignmentValue
	<- identifier (Or identifier)* 

AssignmentSeparator
	<- SPACE '=' SPACE

Or
	<- SPACE '|' SPACE

ClassDeclaration
	<- modifier? ClassIdentifier ExtendClause? ImplementClause? ClassBody 

InterfaceDeclaration
	<- modifier? InterfaceIdentifier ExtendClause? InterfaceBody 

ModuleIdentifier
	<- 'module' SPACE <identifier> { p.NewModule(text) }

ClassIdentifier
	<- 'class' SPACE <fakeIdentifier> { p.NewClass(text) }

InterfaceIdentifier
	<- 'interface' SPACE <fakeIdentifier> { p.NewInterface(text) }

ClassBody
	<- block_start
			(VariableDeclaration / FuncDeclaration)* 
	   block_end

InterfaceBody
	<- block_start
			(VariableDeclaration 
			 / FuncDeclaration 
			 / (FuncType DeclarationSeperator)
			 / IndexDeclaration)* 
	   block_end

IndexDeclaration
	<- square_start
			VariableDifinition
	   square_end
	   TypeSeperator
	   Type
	   DeclarationSeperator

ExtendClause
	<- SPACE 'extends' SPACE fakeIdentifier

ImplementClause
	<- SPACE 'implements' SPACE identifier

VariableDeclaration
	<- <modifier? VariableDifinition> DeclarationSeperator {p.NewVariable(buffer[begin:end])}

FuncDeclaration
	<- <modifier? identifier FuncType> DeclarationSeperator {p.NewFunction(buffer[begin:end])}

Type
	<- BasicType (SPACE '|' SPACE BasicType)*

BasicType
	<- ArrayType
		/ FixedArrayType
		/ ObjectType 
		/ ParenFunc
		/ FuncType 
		/ "number" 
		/ "boolean"
		/ "string"
		/ "function"
		/ "any"
		/ LiteralType
		/ fakeIdentifier

ParenFunc
	<- paren_start 
			FuncType
	   paren_end 

FuncType
	<- paren_start 
			VariableDifinition? (ArgumentSeperator VariableDifinition)*
	   paren_end 
	   (( FuncReturn / TypeSeperator) Type)?

FixedArrayType
	<- square_start
			identifier (ArgumentSeperator identifier)*
	   square_end

ArrayType
	<-  ("number" 
		/ "boolean"
		/ "string"
		/ "function"
		/ "Function"
		/ "any"
		/ identifier
		/ ObjectType) '[]'

ObjectType
	<- block_start
			(ObjectKey TypeSeperator Type DeclarationSeperator)*
	   block_end

ObjectKey
	<- identifier / SquareKey

SquareKey
	<- square_start
			identifier TypeSeperator Type
	   square_end   

LiteralType
	<- ['] ( [[A-Z]] / [_.$] ) ( [[A-Z]] / [0-9]  /[_?.$<>] / '-' )* [']

VariableDifinition
	<- identifier TypeSeperator Type

TypeSeperator
	<- SPACE ':' SPACE

FuncReturn
	<- SPACE '=>' SPACE

ArgumentSeperator
	<- SPACE ',' SPACE

DeclarationSeperator
	<- ((';' / eol)? SPACE)?

keywords
	<- 'module' / 'class' / 'interface' / 'extends' / 'implements'
		/ 'boolean' / 'number' / 'string' / 'void'

modifier
	<- (('declare' / 'export' / 'private' / 'static' 
	    / 'import' / "var" / "function") SPACE)+

fakeIdentifier
	<- ([[A-Z]]/[_.$]) ([[A-Z]]/[0-9]/[_?.$<>])*

identifier
	<- ([[A-Z]]/[_.$]) ([[A-Z]]/[0-9]/[_?.$])*

separator
	<- [:;(){},\[\]=>]

Comment
	<- LineComment / BlockComment

LineComment
	<- '//' (!eol .)* eol

BlockComment
	<- '/*' (!'*/' .)* '*/'

block_start
	<- SPACE '{' SPACE

block_end
	<- SPACE '}' SPACE

paren_start
	<- SPACE '(' SPACE

paren_end
	<- SPACE ')' SPACE

square_start
	<- SPACE '[' SPACE

square_end
	<- SPACE ']' SPACE

SPACE
	<- spacing* 

ws				<- [ \t\n\r]
spacing         <- [ \t\n\r] / Comment
eol             <- '\n'
eof             <- !.
