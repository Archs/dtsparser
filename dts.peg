package main

import "github.com/Archs/dtsparser/dts"

type Parser Peg {
	dts.DTS
}

start
	<- space (ModuleDeclaration 
	          / ClassDeclaration 
	          / InterfaceDeclaration
	          / AssignmentDeclaration
	          / VariableDeclaration
	          / FuncDeclaration )+ eof

ModuleDeclaration
	<- modifier? ModuleIdentifier ModuleBody

ModuleIdentifier
	<- 'module' space <fakeIdentifier> { p.NewModule(text) }

ModuleBody
	<- block_start 
		   (ClassDeclaration
		   	/ InterfaceDeclaration
		   	/ ModuleDeclaration
			/ VariableDeclaration
			/ FuncDeclaration
			/ AssignmentDeclaration)*
	   block_end

ClassDeclaration
	<- modifier? ClassIdentifier ExtendClause? ImplementClause? ClassBody 

ClassIdentifier
	<- 'class' space <GenericIdentifier> { p.NewClass(text) }

ClassBody
	<- block_start
			(VariableDeclaration / FuncDeclaration)* 
	   block_end

InterfaceDeclaration
	<- modifier? InterfaceIdentifier ExtendClause? InterfaceBody 

InterfaceIdentifier
	<- 'interface' space <GenericIdentifier> { p.NewInterface(text) }

InterfaceBody
	<- block_start
			(VariableDeclaration 
			 / FuncDeclaration 
			 / (FuncType end)
			 / IndexDeclaration)* 
	   block_end

AssignmentDeclaration
	<- assignmentModifier? Identifier? equals AssignmentValue end

assignmentModifier
	<- (('declare' / 'export' / 'private' / 'static' 
	    / 'import' / "var" / "function" / 'type') space)+

AssignmentValue
	<- Value (or Value)* 

IndexDeclaration
	<- square_start
			VariableDifinition
	   square_end
	   hasType
	   Type
	   end

ExtendClause
	<- space 'extends' space GenericIdentifier (and GenericIdentifier)*

ImplementClause
	<- space 'implements' space Identifier (and Identifier)*

VariableDeclaration
	<- <modifier? VariableDifinition> end {p.NewVariable(buffer[begin:end])}

FuncDeclaration
	<- <modifier? Identifier FuncType> end {p.NewFunction(buffer[begin:end])}

Type
	<- ('typeof' space)? (BasicType (or BasicType)*)

GenericIdentifier
	<- Identifier GenericForm?

GenericType
	<- Identifier GenericForm

GenericForm
	<- g_start
	   	genericDecl (and genericDecl)*
	   g_end

genericDecl
	<- Type (space 'extends' space Type)?

BasicType
	<- ArrayType
		/ GenericType
		/ FixedArrayType
		/ ObjectType 
		/ ParenFunc
		/ FuncType 
		/ "number" 
		/ "boolean"
		/ "string"
		/ "function"
		/ "any"
		/ LiteralType
		/ Identifier

ParenFunc
	<- paren_start 
			FuncType
	   paren_end 

FuncType
	<- GenericForm? paren_start 
			VariableDifinition? (and VariableDifinition)*
	   paren_end 
	   (( return / hasType) Type)?

FixedArrayType
	<- square_start
			Type (and Type)*
	   square_end

ArrayType
	<-  (GenericType
		/ FixedArrayType
		/ ObjectType 
		/ ParenFunc
		/ FuncType 
		/ "number" 
		/ "boolean"
		/ "string"
		/ "function"
		/ "any"
		/ LiteralType
		/ Identifier) '[]'+

ObjectType
	<- block_start
			((ObjectKey hasType Type end)
			 / FuncDeclaration
			 / FuncType end)*
	   block_end

ObjectKey
	<- Identifier / SquareKey

SquareKey
	<- square_start
			Identifier hasType Type
	   square_end   

LiteralType
	<- String

VariableDifinition
	<- Identifier hasType Type

keywords
	<- 'module' / 'class' / 'interface' / 'extends' / 'implements'
		/ 'boolean' / 'number' / 'string' / 'void'

modifier
	<- (('declare' / 'export' / 'private' / 'static' 
	    / 'import' / "var" / "function") space)+

fakeIdentifier
	<- ([[A-Z]]/[_.$'"]) ([[A-Z]]/[0-9]/[_?.$<>'"])*

Identifier
	<- ([[A-Z]]/[_.$]) ([[A-Z]]/[0-9]/[_?.$])*

Value
	<- Type / FuncCall / Number / String

# Value for function call
FuncCall
	<- Identifier
	   paren_start
	   		Value (and Value)*
	   paren_end

Number
	<- [+-]? [0-9] [.]? [0-9]*

String
	<- ['] ( [[A-Z]] / [_.$] ) ( [[A-Z]] / [0-9]  /[_?.$<>] / '-' )* [']
	  /["] ( [[A-Z]] / [_.$] ) ( [[A-Z]] / [0-9]  /[_?.$<>] / '-' )* ["]


separator
	<- [:;(){},\[\]=>]

return
	<- space '=>' space

hasType
	<- space ':' space

equals
	<- space '=' space

or
	<- space '|' space

and
	<- space ',' space

end
	<- ((';' / eol)? space)?

block_start
	<- space '{' space

block_end
	<- space '}' space

paren_start
	<- space '(' space

paren_end
	<- space ')' space

square_start
	<- space '[' space

square_end
	<- space ']' space

g_start
	<- space '<' space

g_end
	<- space '>' space

space
	<- spacing* 

comment
	<- lineComment / blockComment

lineComment
	<- '//' (!eol .)* eol

blockComment
	<- '/*' (!'*/' .)* '*/'

ws				<- [ \t\n\r]
spacing         <- [ \t\n\r] / comment
eol             <- '\n'
eof             <- !.
