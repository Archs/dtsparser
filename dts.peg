package main

import "github.com/Archs/dtsparser/dts"

type Parser Peg {
	dts.DTS
}

start
	<- SPACE (ModuleDeclaration / ClassDeclaration / InterfaceDeclaration )+ eof

ModuleDeclaration
	<- modifier? ModuleIdentifier ModuleBody

ModuleBody
	<- block_start 
		   (ClassDeclaration
		   	/ InterfaceDeclaration
		   	/ ModuleDeclaration
			/ VariableDeclaration
			/ FuncDeclaration)*
	   block_end

ClassDeclaration
	<- modifier? ClassIdentifier ExtendClause? ImplementClause? ClassBody 

InterfaceDeclaration
	<- modifier? InterfaceIdentifier ExtendClause? InterfaceBody 

ModuleIdentifier
	<- 'module' SPACE <identifier> { p.NewModule(text) }

ClassIdentifier
	<- 'class' SPACE <identifier> { p.NewClass(text) }

InterfaceIdentifier
	<- 'interface' SPACE <identifier> { p.NewInterface(text) }

ClassBody
	<- block_start
			(VariableDeclaration / FuncDeclaration)* 
	   block_end

InterfaceBody
	<- block_start
			(VariableDeclaration 
			 / FuncDeclaration 
			 / (FuncType DeclarationSeperator))* 
	   block_end

ExtendClause
	<- SPACE 'extends' SPACE identifier

ImplementClause
	<- SPACE 'implements' SPACE identifier

VariableDeclaration
	<- <modifier? VariableDifinition> DeclarationSeperator {p.NewVariable(buffer[begin:end])}

FuncDeclaration
	<- <modifier? identifier FuncType> DeclarationSeperator {p.NewFunction(buffer[begin:end])}

Type
	<- BasicType (SPACE '|' SPACE BasicType)*

BasicType
	<- ArrayType
		/ GenericType
		/ ObjectType 
		/ FuncType 
		/ "number" 
		/ "boolean"
		/ "string"
		/ "function"
		/ "any"
		/ LiteralType
		/ identifier

FuncType
	<- paren_start 
			VariableDifinition? (ArgumentSeperator VariableDifinition)*
	   paren_end 
	   (( FuncReturn / TypeSeperator) Type)?

ArrayType
	<-  ("number" 
		/ "boolean"
		/ "string"
		/ "function"
		/ "Function"
		/ "any"
		/ identifier
		/ ObjectType) '[]'

GenericType
	<- identifier '<' identifier '>'

ObjectType
	<- block_start
			VariableDeclaration*
	   block_end

LiteralType
	<- ['] identifier [']

VariableDifinition
	<- identifier TypeSeperator Type

TypeSeperator
	<- SPACE ':' SPACE

FuncReturn
	<- SPACE '=>' SPACE

ArgumentSeperator
	<- SPACE ',' SPACE

DeclarationSeperator
	<- ((';' / eol)? SPACE)?

keywords
	<- 'module' / 'class' / 'interface' / 'extends' / 'implements'
		/ 'boolean' / 'number' / 'string' / 'void'

modifier
	<- (('declare' / 'export' / 'private' / 'static' / 'import' / "var" / "function") SPACE)+

identifier
	<- ([[A-Z]]/[_.$]) ([[A-Z]]/[0-9]/[_?.$<>])*

separator
	<- [:;(){},\[\]=>]

Comment
	<- LineComment / BlockComment

LineComment
	<- '//' (!eol .)* eol

BlockComment
	<- '/*' (!'*/' .)* '*/'

block_start
	<- SPACE '{' SPACE

block_end
	<- SPACE '}' SPACE

paren_start
	<- SPACE '(' SPACE

paren_end
	<- SPACE ')' SPACE

SPACE
	<- spacing* 

ws				<- [ \t\n\r]
spacing         <- [ \t\n\r] / Comment
eol             <- '\n'
eof             <- !.
